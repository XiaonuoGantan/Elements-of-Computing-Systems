// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU.  Computes a pre-defined set of functions out = f(x,y)
 * where x and y are two 16-bit inputs. The function f is selected 
 * by a set of 6 control bits denoted zx, nx, zy, ny, f, no.
 * The ALU operation can be described using the following pseudocode:
 *     if zx=1 set x = 0       // 16-bit zero constant
 *     if nx=1 set x = !x      // Bit-wise negation
 *     if zy=1 set y = 0       // 16-bit zero constant
 *     if ny=1 set y = !y      // Bit-wise negation
 *     if f=1  set out = x + y // Integer 2's complement addition
 *     else    set out = x & y // Bit-wise And
 *     if no=1 set out = !out  // Bit-wise negation
 *
 * In addition to computing out, the ALU computes two 1-bit outputs:
 *     if out=0 set zr = 1 else zr = 0 // 16-bit equality comparison
 *     if out<0 set ng = 1 else ng = 0 // 2's complement comparison
 */

CHIP ALU {

    IN  // 16-bit inputs:
        x[16], y[16],
        // Control bits:
        zx, // Zero the x input
        nx, // Negate the x input
        zy, // Zero the y input
        ny, // Negate the y input
        f,  // Function code: 1 for add, 0 for and
        no; // Negate the out output

    OUT // 16-bit output
        out[16],

        // ALU output flags
        zr, // 1 if out=0, 0 otherwise
        ng; // 1 if out<0, 0 otherwise

    PARTS:
    
    //handle x    
    Mux16(a=x, b=false, sel=zx, out=out1x);
    Not16(in=out1x, out=notout1x);
    Mux16(a=out1x, b=notout1x, sel=nx, out=out2x);

    //handle y
    Mux16(a=y, b=false, sel=zy, out=out1y);
    Not16(in=out1y, out=notout1y);
    Mux16(a=out1y, b=notout1y, sel=ny, out=out2y);

    //handle & / +
    And16(a=out2x, b=out2y, out=out3);
    Add16(a=out2x, b=out2y, out=out4);

    //handle output
    Mux16(a=out3, b=out4, sel=f, out=out5);
    Not16(in=out5, out=notout5);
    Mux16(a=out5, b=notout5, sel=no, out=out, out=check, out[15]=MSB);

    // see if out is 0
    Comparator16(a=check, b[0..15]=false, out=zr);

    // check out MSB to see if out < 0
    Comparator(a=MSB, b=true, out=ng);
    
}
